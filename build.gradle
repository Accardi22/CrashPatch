plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.minecraftforge.gradle.forge" version "ddb1eb0"
    id "org.spongepowered.mixin" version "d5f9873"
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
}

version = "1.0"
group = mod_group
archivesBaseName = mod_archives_name

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_22"
    replace "@VERSION@", project.version

    clientRunArgs += "--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker"
    clientRunArgs += "--mixin mixin.bettercrashes.json"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

configurations {
    // Creates an extra configuration that extends from `implementation` to be used later as the configuration that shades libraries
    include
    implementation.extendsFrom include
}

repositories {
    mavenCentral()
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url "https://repo.sk1er.club/repository/maven-releases/" }
    maven { url "https://jitpack.io/" }
}

dependencies {
    include ("gg.essential:loader-launchwrapper:1.1.2") {
        transitive = false
    }
    implementation "gg.essential:essential-1.8.9-forge:1651+release-launch"
    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")
}

jar {
    manifest.attributes(
            "ModSide": "CLIENT",
            "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
            "TweakOrder": "0",
            "MixinConfigs": "mixin.bettercrashes.json"
    )

    enabled = false
}

tasks.reobfJar.dependsOn tasks.shadowJar

shadowJar {
    archiveClassifier.set("")
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    exclude "LICENSE.md"
    exclude "pack.mcmeta"
    exclude "dummyThing"
    exclude "**/module-info.class"
    exclude "*.so"
    exclude "*.dylib"
    exclude "*.dll"
    exclude "*.jnilib"
    exclude "ibxm/**"
    exclude "com/jcraft/**"
    exclude "org/lwjgl/**"
    exclude "net/java/**"

    exclude "META-INF/proguard/**"
    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"
    exclude "META-INF/com.android.tools/**"

    exclude "fabric.mod.json"
}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "mixin.bettercrashes.refmap.json"
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

sourceSets {
    main {
        ext.refMap = "mixin.bettercrashes.refmap.json"
        output.resourcesDir = file("${buildDir}/classes/java/main")
    }
}